plugins {
  id "com.ullink.msbuild" version "2.9"
  id "com.ullink.nuget" version "2.8"
}

archivesBaseName = 'RxShim'
version = '1.0'

defaultTasks('nugetPack')

assemblyInfoPatcher {
    version = project.version + '.0.0'
    enabled = true
}

msbuild.dependsOn nugetRestore
msbuild {
    solutionFile = 'RxShim.sln'
    configuration = 'Release'
    projectName = 'RxShim'
    inputs.file(project.buildFile)
}

// nuget package for upload to nuget
nugetPack {
    nuspec {
        metadata() {
            version version
            title 'RxShim - Rx to await for .Net 4.0'
            authors 'Paul Betts'
            owners 'Francois Valdy'
            projectUrl('https://github.com/Ullink/rx-shim')
            copyright 'Copyright © Ullink 2015'
            id archivesBaseName
            delegate.description '''Adds the missing GetAwaiter extension methods for IObservable on .Net 4.0 with Microsoft.Bcl'''
            requireLicenseAcceptance false
            dependencies() {
                group() {
                    dependency(id: 'Microsoft.Bcl'      , version: '1.1.10')
                    dependency(id: 'Microsoft.Bcl.Build', version: '1.0.21')
                    dependency(id: 'Rx-Main'            , version: '2.2.5')
                }
            }
        }
        delegate.files() {
            delegate.file(src: msbuild.mainProject.properties.TargetPath, target: 'lib/net40')
        }
   }
}

// nuget package upload, requires API key to be set
nugetPush {
    apiKey = project.properties.nugetApiKey
    nupkgFile = nugetPack.packageFile
}
